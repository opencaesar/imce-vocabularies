@dc:title "Metrology"
@dc:^description "The Metology vocabulary defines the concepts of metrology according to Prof. Rene Dybkaer's 'An Ontology of Property' and the VIM3.
See https://jcgm.bipm.org/vim/en/index.html
See https://www.bipm.org/utils/common/documents/jcgm/JCGM_200_2012.pdf"
@dc:rights "Copyright 2019, by the California Institute of Technology. ALL RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology.\n\nThis software may be subject to U.S. export control laws. By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. User has the responsibility to obtain export licenses, or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons."
@dc:creator "Jet Propulsion Laboratory, California Institute of Technology"
@dc:hasVersion "1.1.0"
@dc:date "2020-10-06"
vocabulary <http://imce.jpl.nasa.gov/foundation/metrology> with # as metrology {

    extends <http://www.w3.org/2002/07/owl>

    @dc:^description "metrology:Thing is the root of the taxonomy of OML entities involved in metrological modeling either as a metrological entity, a number or a component subject of statements involving metrological entities."
    aspect Thing

    @dc:^description "metrology:MetrologicalEntity is the root of the toplevel category of OML concepts corresponding to VIM3 definitions."
    concept MetrologicalEntity :> Thing

    @dc:^description "metrology:Component is the root of the taxonomy of OML entities corresponding to a phenomenon, body or substance that
    can be the subject of:
    - metrological characterizations via quantity properties 
    - metrological measurement processes for obtaining quantity values for such quantity properties."
    @dc:source "The phenomenon, body or substance referred to in the definition of quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
    @dc:source "component [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 3.4, p. 39]"
    aspect Component :> Thing

    @dc:^description "metrology:Number is the root of the taxonomy of OML entities corresponding to a representation of the numerical value of a quantity [VIM3-1.20].
    A numerical quantity value is an OML entity instead of an OML datatype because it can be referenced in the context of a numerical value equation [VIM3-1.25].
    
    Note: The representation of the numerical quantity value is deliberately left open to be specified in library extensions.
    This strategy provides ample latitude for choosing suitable datatypes for representing a numerical quantity value
    optionally with additional information such as measurement precision [VIM3-2.15].
    "
    @dc:source "numerical quantity value [https://jcgm.bipm.org/vim/en/1.20.html]"
    concept Number :> Thing

    @dc:^description "common defining aspect of mutually comparable properties having a magnitude."
    @dc:source "kind of quantity [https://jcgm.bipm.org/vim/en/1.2.html]"
    @dc:source "kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.1, p. 125 and 13.4.1, p. 126]"
    concept QuantityKind :> MetrologicalEntity

    @dc:^description "A QuantityKind for Quantities that are rankable by magnitude, but are neither subtractive nor divisible."
    @dc:source "ordinal kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.2, p. 125]"
    concept OrdinalQuantityKind :> QuantityKind

    @dc:^description "A QuantityKind for Quantities with magnitudes expressed as a measurement unit multiplied by a number."
    @dc:source "unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.3, p. 125]"
    concept UnitaryQuantityKind :> QuantityKind

    @dc:^description "A UnitaryQuantityKind uses a MeasurementUnit as a reference for expressing the magnitude of quantities of this UnitaryQuantityKind."
    @dc:source "metrological unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.2, p. 189]"
    relation entity HasMeasurementUnit [
        from UnitaryQuantityKind
        to MeasurementUnit
        forward hasMeasurementUnit
    ]

    @dc:source "taxonomic relations among unitary kind-of-quantity is an inherent aspect of the concept system in [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, Table 2.13, p. 28 and Figure 13.13, p. 135]"
    relation entity HasGeneralUnitaryQuantityKind [
        from UnitaryQuantityKind
        to UnitaryQuantityKind
        forward hasGeneralUnitaryQuantityKind
        asymmetric
        irreflexive
    ]

    @dc:^description "An IndependentUnitaryQuantityKind is a UnitaryQuantityKind defined independently of any other UnitaryQuantityKind."
    @dc:source "implicit as a quantity that is not a derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
    @dc:source "base unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.9, p. 130]"
    concept IndependentUnitaryQuantityKind :> UnitaryQuantityKind

    @dc:^description "A DerivedQuantityKind is a UnitaryQuantityKind defined as the product of powers of UnitaryQuantityKinds."
    @dc:source "derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
    @dc:source "derived unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.11, p. 130-1]"
    concept DerivedUnitaryQuantityKind :> UnitaryQuantityKind

    relation entity HasQuantityFactor [
        from DerivedUnitaryQuantityKind
        to UnitaryQuantityKind
        forward hasQuantityFactor
        irreflexive
    ]

    scalar property NumeratorOfQuantityFactor [
        domain HasQuantityFactor
        range xsd:int
    ]

    scalar property DenominatorOfQuantityFactor [
        domain HasQuantityFactor
        range xsd:int
    ]

    @dc:^description "A SystemOfUnitaryQuantityKinds is a collection of UnitaryQuantityKinds that can be partitioned into two groups:
    base and derived quantity kinds. Note that this partitioning is independent of how the algebraic definition of the UnitaryQuantityKinds
    (i.e IndependentUnitaryQuantityKind or DerivedUnitaryQuantityKind)."
    @dc:source "system of quantities [https://jcgm.bipm.org/vim/en/1.3.html]"
    @dc:source "system of unitary kinds-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.7, p 129]"
    concept SystemOfUnitaryQuantityKinds :> MetrologicalEntity

    relation entity SystemHasUnitaryQuantityKind [
        from SystemOfUnitaryQuantityKinds
        to UnitaryQuantityKind
        forward systemHasUnitaryQuantityKind
    ]

    scalar property IsBaseQuantityKind [
        domain SystemHasUnitaryQuantityKind
        range xsd:boolean
    ]

    concept Reference :> MetrologicalEntity

    @dc:source "measurement procedure [https://jcgm.bipm.org/vim/en/2.6.html]"
    @dc:source "measurement procedure [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 14.4.3, p 139]"
    concept MeasurementProcedure :> Reference

    @dc:source "reference material [https://jcgm.bipm.org/vim/en/5.13.html]"
    @dc:source "reference material [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 5.1, p 47]"
    concept ReferenceMaterial :> Reference

    @dc:source "measurement unit [https://jcgm.bipm.org/vim/en/1.9.html]"
    @dc:source "metrological unit, measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.12, p 189]"
    concept MeasurementUnit :> Reference

    @dc:source "taxonomic relations among measurement units is an inherent aspect of the concept system in [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, Table 2.13, p. 28 and Figure 13.13, p. 135]"
    relation entity HasGeneralMeasurementUnit [
        from MeasurementUnit
        to MeasurementUnit
        forward hasGeneralMeasurementUnit
        asymmetric
        irreflexive
    ]

    @dc:^description "An IndependentMeasurementUnit is a MeasurementUnit defined independently of any other MeasurementUnits."
    @dc:source "base unit [https://jcgm.bipm.org/vim/en/1.10.html]"
    @dc:source "base metrological unit, base unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.15, p 190-1]"
    concept IndependentMeasurementUnit :> MeasurementUnit

    @dc:^description "A DerivedMeasurementUnit is a MeasurementUnit defined as the product of powers of other MeasurementUnits."
    @dc:source "derived unit [https://jcgm.bipm.org/vim/en/1.11.html]"
    @dc:source "derived metrological unit, derived unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.17, p 192]"
    concept DerivedMeasurementUnit :> MeasurementUnit

    relation entity HasUnitFactor [
        from DerivedMeasurementUnit
        to MeasurementUnit
        forward hasUnitFactor
        irreflexive
    ]

    scalar property NumeratorOfUnitFactor [
        domain HasUnitFactor
        range xsd:int
    ]

    scalar property DenominatorOfUnitFactor [
        domain HasUnitFactor
        range xsd:int
    ]

    @dc:^description "A SystemOfMetrologicalUnits is a collection of MeasurementUnits that can be partitioned into two groups:
    base and derived measurement units. Note that this partitioning is independent of how the algebraic definition of the MeasurementUnits
    (i.e IndependentMeasurementUnit or DerivedMeasurementUnit)."
    @dc:source "system of units [https://jcgm.bipm.org/vim/en/1.13.html]"
    @dc:source "system of metrological units [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.25, p 195]"
    concept SystemOfMetrologicalUnits :> MetrologicalEntity

    relation entity SystemHasMetrologicalUnit [
        from SystemOfMetrologicalUnits
        to MeasurementUnit
        forward systemHasMetrologicalUnit
    ]

    scalar property IsBaseMetrologicalUnit [
        domain SystemHasMetrologicalUnit
        range xsd:boolean
    ]

    @dc:source "quantity value [https://jcgm.bipm.org/vim/en/1.19.html]"
    @dc:source "quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.7, p 162]"
    concept QuantityValue :> MetrologicalEntity
    
    relation entity HasNumber [
        from QuantityValue
        to Number
        forward hasNumber
    ]

    relation entity HasReference [
        from QuantityValue
        to Reference
        forward hasReference
    ]

    @dc:source "ordinal quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.9, p 163]"
    concept OrdinalQuantityValue :> QuantityValue [
        restricts all relation hasReference to MeasurementProcedure
    ]

    @dc:source "unitary quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.10, p 163]"
    concept UnitaryQuantityValue :> QuantityValue [
        restricts all relation hasReference to MeasurementUnit
    ]

    @dc:source "quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
    @dc:source "quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
    relation entity Quantity [
        from Component
        to QuantityValue
        forward quantity
    ]

    relation entity HasQuantityKind [
        from Quantity
        to QuantityKind
        forward hasQuantityKind
    ]

    @dc:source "ordinal quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.16, p 116]"
    relation entity OrdinalQuantity :> Quantity [
        from Component
        to OrdinalQuantityValue
        forward ordinalQuantity
        restricts all relation hasQuantityKind to OrdinalQuantityKind
    ]

    @dc:source "unitary quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.17, p 116-7]"
    relation entity UnitaryQuantity :> Quantity [
        from Component
        to UnitaryQuantityValue
        forward unitaryQuantity
        restricts all relation hasQuantityKind to UnitaryQuantityKind
    ]
}