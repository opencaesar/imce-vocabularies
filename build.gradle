/* 
 * The Maven coordinates for the project artifact
 */
ext.title = "IMCE Vocabularies"
description='The set of IMCE vocabularies'
group = 'io.opencaesar.ontologies'
version = '3.1.0'

/*
 * Properties
 */
ext.bikeshed = 'build/bikeshed'

apply from: "${rootDir}/gradle/maven-deployment.gradle"

/* 
 * The Gradle task dependencies 
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'io.opencaesar.owl:owl-reason-gradle:+'
        classpath 'io.opencaesar.adapters:oml2owl-gradle:1.+'
        classpath 'io.opencaesar.oml:oml-bikeshed-gradle:1.+'
        classpath 'io.opencaesar.oml:oml-merge-gradle:1.+'
    }
}

/*
 * The repositories to look up OML dependencies in
 */
repositories {
    mavenLocal()
    mavenCentral()
}

/*
 * The configuration of OML dependencies
 */
configurations {
    oml
}

/*
 * Dependency versions
 */
ext { 
    metrologyVersion = '5.+'
}

/*
 * The OML dependencies
 */
dependencies {
    oml "io.opencaesar.ontologies:metrology-vocabularies:$metrologyVersion"
}

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask) {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OML catalog title
    inputCatalogTitle = project.title
    // OML catalog version
    inputCatalogVersion = project.version
    // Root ontology IRI
    rootOntologyIri='http://imce.jpl.nasa.gov/foundation/bundle'
    // OWL folder
    outputFolderPath = file("$bikeshed")
    // Publish URL
    publishUrl = 'https://opencaesar.github.io/imce-vocabularies/'
}

/*
 * A task to render the Bikeshed specification to HTML
 */
task generateDocs(type: Exec, dependsOn: omlToBikeshed) {
	inputs.files(fileTree("$bikeshed").include('**/*.bs'))
	outputs.files(fileTree("$bikeshed").include('**/*.html'))
	if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
		commandLine "$bikeshed/publish.bat"
	} else {
		commandLine "$bikeshed/publish.sh"
	}
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: downloadDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
    // OWL catalog
    catalogPath = file('build/owl/catalog.xml')
    // Input ontology IRI to reason on
    inputOntologyIri = 'http://imce.jpl.nasa.gov/foundation/bundle'
    // Entailment statements to generate and the ontologies to persist them in
    specs = [
        'http://imce.jpl.nasa.gov/foundation/bundle/classes = ALL_SUBCLASS',
        'http://imce.jpl.nasa.gov/foundation/bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
        'http://imce.jpl.nasa.gov/foundation/bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
    ]
    // Junit error report
    reportPath = file('build/reports/reasoning.xml')
}

/*
 * A task to build the project, which executes several tasks together
 */
tasks.named('build') {
    dependsOn owlReason
}

/*
 * Publish to Maven spec
 */
 
task omlZip(type: Zip) {
    from file('src/oml')
    include "**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
}

publishing.publications.maven.artifact omlZip

/*
 * Integration with the Eclipse IDE
 */ 

apply plugin: 'eclipse'

eclipse {
    synchronizationTasks downloadDependencies
}